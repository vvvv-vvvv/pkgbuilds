# Maintainer: forest

pkgname=tgstation-server
pkgver=5.17.0
pkgrel=1
pkgdesc="A production scale tool for BYOND server management"
arch=('x86_64') # purposefully not any
url="https://github.com/tgstation/tgstation-server"
license=('AGPL3')
depends=('aspnet-runtime-6.0')
makedepends=('dotnet-sdk' 'ed' 'git' 'npm')
install=tgstation-server.install
source=("${pkgname}::git+https://github.com/tgstation/tgstation-server#tag=tgstation-server-v${pkgver}"
  "tgs-configure"
  "tgstation-server.service"
  "tgstation-server.sysusers"
  "tgstation-server.tmpfiles")
b2sums=('SKIP'
        'f7e6b84852494b3b8e1fe6b915df7ced78d65cff4978ba4482f01139ef17d1534b292f70ad2e84381f5e1a829058033aee5ac680342784ed91fc9ff324f67cfa'
        'f711b5d2c8fece9ad358aa5975a93c833a66ce0d78886549902e9ae0f4af2c8a26fff61aed5fc342da2ae1fb7182f6ba8a5d222ded81bd6b684099efec6812bc'
        'e7bdd81fe7b7cd9b6e83484d5887b616759ace86e20e503ead704e7f68f73c4f354f8ebd21fa8295518a9fcd66958f98af653dd3048f9d93d9bec6ad63437587'
        '93a054546a26a131a7a93d61b4b18b875c0aedb5a842eca91f159f2fdc588951fc0bf21b388c904d6818d3d656aad4e1bea51b231b3505e3fb5997d99984978d')

prepare() {
  cd "${srcdir}/${pkgname}"
  ed -s src/Tgstation.Server.Host/Controllers/AdministrationController.cs <<< $'/^\t\tpublic.*Update.*$/+a
\t\t\treturn UnprocessableEntity("Updating disabled by packager");
#pragma warning disable CS0162
.
/^\t\t}$/a
#pragma warning restore CS0162
.
w'
  sed -i '/^script_full_path=/c\\script_full_path="$(dirname "$(readlink -f "$0")")"' src/Tgstation.Server.Host.Console/tgs.sh
}

build() {
  cd "${srcdir}/${pkgname}"
  export DOTNET_CLI_TELEMETRY_OPTOUT=1
  dotnet restore
  dotnet publish src/Tgstation.Server.Host.Console --no-self-contained --use-current-runtime -p:DebugType=none -c ReleaseNoWindows -o artifacts
  dotnet publish src/Tgstation.Server.Host --no-self-contained --use-current-runtime -p:DebugType=none -c ReleaseNoWindows -o artifacts/lib/Default
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm644 "${srcdir}/tgstation-server.service" "${pkgdir}/usr/lib/systemd/system/tgstation-server.service"
  install -Dm644 "${srcdir}/tgstation-server.sysusers" "${pkgdir}/usr/lib/sysusers.d/tgstation-server.conf"
  install -Dm644 "${srcdir}/tgstation-server.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/tgstation-server.conf"
  mkdir -p "${pkgdir}/usr/lib"
  mkdir -p "${pkgdir}/usr/bin"
  cp -dr --no-preserve='ownership' artifacts "${pkgdir}/usr/lib/${pkgname}"
  ln -s "/usr/lib/${pkgname}/tgs.sh" "${pkgdir}/usr/bin/tgs"
  install -Dm755 "${srcdir}/tgs-configure" "${pkgdir}/usr/bin/tgs-configure"
  install -Dm644 "${srcdir}/${pkgname}/src/Tgstation.Server.Host/appsettings.yml" "${pkgdir}/usr/share/doc/${pkgname}/examples/appsettings.yml"
}

# vim: ts=2 sw=2 et:
